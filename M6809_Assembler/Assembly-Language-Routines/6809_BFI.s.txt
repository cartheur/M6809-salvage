	.macro	SEC
		ORCC	#$01
	.endm

;
;	Title:			Bit Field Insertion
;	Name:			BFI
;
;	Purpose:		Inserts a field of bits which is
;				normalized to bit 0 into a 16-bit word.
;
;			NOTE:	IF THE REQUESTED FIELD IS TOO LONG, 
;				THEN ONLY THE BITS THROUGH BIT 15 
;				WILL BE INSERTED.
;				FOR EXAMPLE, IF A 4 BIT FIELD IS 
;				REQUESTED STARTING AT BIT 15,
;				THEN 0NLY THE FIRST BIT WILL BE INSERTED
;				AT BIT 15.
;
;	Entry:			TOP OF STACK
;				High byte of return address
;				Low  byte of return address
;				Bit position at which inserted field will start (0..15) 
;				Number of bits in the field (1..16) 
;				High byte of value to insert
;				Low  byte of value to insert
;				High byte of value
;				Low  byte of value
;
;	Exit:			Register D = Value with field inserted
;
;	Registers Used:		A,B,CC,U,X
;
;	Time:			91 cycles overhead plus 
;				(30 * lowest bit position) cycles
;	Size:			Program 67 bytes
;
BFI:
;	LDU	,S		; SAVE RETURN ADDRESS
;
;	EXIT WITH DATA AS RESULT IF WIDTH OF FIELD IS ZERO
;
	LDD	6,S		; GET DATA
	TST	3,S		; CHECK FIELD WIDTH
	BEQ	EXITBF		; BRANCH (EXIT) IF FIELD WIDTH IS ZERO
				; RESULT IN D IS ORIGINAL DATA
;
;	USE FIELD WIDTH TO OBTAIN MASK FROM ARRAY
;	MASK HAS A NUMBER OF RIGHT-JUSTIFIED 0 BITS GIVEN BY FIELD WIDTH
;
	LDA	3,S		; GET FIELD WIDTH
	DECA			; CONVERT FIELD WIDTH TO ARRAY INDEX
	ANDA	#$0F		; MAKE SURE INDEX IS U TO 15
	ASLA			; MULTIPLY BY 2 SINCE MASKS ARE WORD-LENGTH
;
; will not assemble PCR
;
;	LEAX	MSKARY,PCR	; GET BASE ADDRESS 0F MASK ARRAY

	LDX	A,X		; GET MASK FROM ARRAY
;
;	SHIFT MASK AND FIELD TO BE INSERTED LEFT TO
;	ALIGN THEM WITH THE FIELD'S LOWEST BIT POSITION
;
	LDA	2,S		; GET LOWEST BIT POSITION
	ANDA	#$0F		; BE SURE POSITION IS 0 TO 15
	BEQ	INSERT		; BRANCH IF POSITION IS 0 AND NO SHIFTING
				; IS NECESSARY
	STA	,S		; SAVE LOWEST POSITION IN STACK FOR USE
				; AS COUNTER
	TFR	X,D		; MOVE MASK TO REGISTER D FOR SHIFTING
SHFTLP:
	SEC			; FILL MASK WITH ONES
	ROLB			; SHIFT LOW BYTE OF MASK LEFT, PUTTING A
				; 1 IN BIT 0
	ROLA			; SHIFT  HIGH BYTE OF MASK LEFT
	ASL	5,S		; SHIFT LOW BYTE OF INSERT VALUE LEFT
	ROL	4,S		; SHIFT HIGH BYTE OF INSERT VALUE LEFT
	DEC	,S
	BNE	SHFTLP		; CONTINUE UNTIL INSERT VALUE'S LEAST
				; SIGNIFICANT BIT IS IN LOWEST BIT
				; POSITION
;
; USE MASK TO CLEAR FIELD, THEN OR IN INSERT VALUE
;
INSERT:
	ANDA	6,S		; AND HIGH BYTE OF VALUE WITH MASK
	ANDB	7,S		; AND LOW BYTE OF VALUE WITH MASK
	ORA	4,S		; OR IN HIGH BYTE OF INSERT VALUE
	ORB	5,S		; OR IN LOW BYTE OF INSERT VALUE
;
;	REMOVE PARAMETERS FROM STACK AND EXIT
;
EXITBF:
	LEAS	8,S		; REMOVE PARAMETERS FROM STACK
	JMP	,U		; EXIT TO RETURN ADDRESS
;
;	MASK ARRAY USED TO CLEAR THE BIT FIELD INITIALLY
;	HAS 0 BITS RIGHT-JUSTIFIED IN 1 TO 15 BIT POSITIONS
MSKARY:
	FDB	1111111111111110B
	FDB	1111111111111100B
	FDB	1111111111111000B
	FDB	1111111111110000B
	FDB	1111111111100000B
	FDB	1111111111000000B
	FDB	1111111110000000B
	FDB	1111111100000000B
	FDB	1111111000000000B
	FDB	1111110000000000B
	FDB	1111100000000000B
	FDB	1111000000000000B
	FDB	1110000000000000B
	FDB	1100000000000000B
	FDB	1000000000000000B

;
;	SAMPLE EXECUTION
;
SC4B:


	LDA	POS		; GET LOWEST BIT POSITION OF FIELD
	LDB	NBITS		; GET FIELD WIDTH IN BITS
	LDX	VALINS		; GET VALUE TO INSERT
	LDY	VAL		; GET VALUE
	PSHS	A,B,X,Y		; SAVE PARAMETERS IN STACK
	JSR	BFI		; INSERT BIT FIELD
				; RESULT FOR VAL=1234H, VALINS=0EH,
				; NBITS = 4, POS = 0CH IS
				; REGISTER D = E234H
	BRA	SC4B
;
;	DATA
;
VAL	FDB	$1234		; DATA VALUE
VALINS	FDB	$000E		; VALUE TO INSERT	
NBITS	FDB	4		; FIELD WIDTH IN BITS
POS	FDB	$0C		; LOWEST BIT POSITION IN FIELD		

	END


